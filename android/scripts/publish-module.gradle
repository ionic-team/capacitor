apply plugin: 'maven-publish'
apply plugin: 'signing'

def LIB_VERSION = System.getenv('CAP_VERSION')

task androidSourcesJar(type: Jar) {
    dependsOn(':bundleReleaseAar')
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = 'com.capacitorjs'
version = LIB_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Coordinates
                groupId 'com.capacitorjs'
                artifactId 'core'
                version LIB_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                // POM Data
                pom {
                    name = 'core'
                    description = 'Capacitor Android Core Native Library'
                    url = 'https://github.com/ionic-team/capacitor'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/ionic-team/capacitor/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            name = 'Ionic'
                            email = 'hi@ionic.io'
                        }
                    }

                    // Version Control Info
                    scm {
                        connection = 'scm:git:github.com:ionic-team/capacitor.git'
                        developerConnection = 'scm:git:ssh://github.com:ionic-team/capacitor.git'
                        url = 'https://github.com/ionic-team/capacitor/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
